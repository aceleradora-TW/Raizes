buildscript {
    ext {
        springBootVersion = '2.0.4.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "org.postgresql:postgresql:42.2.5"
    }
}

plugins {
    id "org.flywaydb.flyway" version "5.1.4"
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

dependencies {
    compile "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
    compile "org.springframework.boot:spring-boot-starter-thymeleaf:${springBootVersion}"
    compile "org.springframework.boot:spring-boot-starter-data-jpa:${springBootVersion}"
    compile "org.springframework.boot:spring-boot-devtools:${springBootVersion}"
    compile "com.h2database:h2"
    compile "org.postgresql:postgresql:42.2.5"

    testCompile 'junit:junit:4.12'
    testCompile "org.mockito:mockito-core:2.21.0"
}

repositories {
    mavenCentral()
}

task startFromJar(type: Exec, dependsOn: assemble) {
    description = "Inicia aplicacao em plano de fundo, utilizando o jar gerado pela task :app:assemble"

    commandLine "${rootDir}/scripts/local-test-server", "start", "$buildDir/libs/app.jar", "$buildDir"
}

task stopRunningJar(type: Exec) {
    description = "Para aplicacao que foi iniciada por :app:startFromJar"

    commandLine "${rootDir}/scripts/local-test-server", "stop", "$buildDir/libs/app.jar", "$buildDir"
}

flyway {

    def host = System.getenv("ESQUELETO_DB_HOST") ?: 'localhost'
    def port = System.getenv("ESQUELETO_DB_PORT") ?: 5432
    def name = System.getenv("ESQUELETO_DB_NAME") ?: 'esqueleto'

    url = "jdbc:postgresql://$host:$port/$name"
    user = System.getenv("ESQUELETO_DB_USER") ?: 'postgres'
    password = System.getenv("ESQUELETO_DB_PASSWORD") ?: 'esqueleto'
}

build.mustRunAfter('clean')
